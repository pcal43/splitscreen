apply plugin: 'java'
apply plugin: 'fabric-loom'
apply plugin: 'com.modrinth.minotaur'
apply plugin: 'net.darkhax.curseforgegradle'

buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        // This has to be on the classpath so we can delcare a task with it below;
        // this is the only way to do that in the applied script.
        // The new curseforgegradle plugin does not have a DSL config.
        classpath "net.darkhax.curseforgegradle:CurseForgeGradle:${curseforgegradle_plugin_version}"
    }
}


base {
    archivesName = "${project.archives_base_name}-fabric"
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"
    implementation project(":common")
}

processResources {
    // Substitute the mod version into fabric.mod.json
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

project.afterEvaluate {
    sourceSets.named('main').configure { sourceSet ->
        sourceSet.java.srcDir(project(":common").file("src/main/java"))
        sourceSet.resources.srcDir(project(":common").file("src/main/resources"))
    }
}

loom {
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

jar {
    // copy LICENSE from the repo root and rename it to avoid conflicts with other mods
    from("LICENSE") {
        rename { "${it}_${archivesName}"}
    }
}

// Publishing configuration
modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "${project.modrinth_projectId}"
    versionNumber = project.mod_version
    versionType = "release"
    uploadFile = remapJar
    changelog = "<p><a href='${project.github_projectUrl}/releases/tag/${project.mod_version}'>${project.github_projectUrl}/releases/tag/${project.mod_version}</a></p>"
    gameVersions = ["${project.minecraft_version}"]
    loaders = ["fabric"]
    dependencies {
        required.project "fabric-api"
    }
}

import net.darkhax.curseforgegradle.TaskPublishCurseForge
tasks.register('publishCurseForge', TaskPublishCurseForge) {
    // API Key from environment or fallback
    apiToken = System.getenv("CURSEFORGE_TOKEN") ?: 'CURSEFORGE_TOKEN NOT_SET'

    // Configure the main artifact
    // The plugin automatically detects Fabric & Java version from your Loom setup
    def mainFile = upload(project.curseforge_projectId, remapJar)

    mainFile.releaseType = "release"
    mainFile.changelog = "${project.github_projectUrl}/releases/tag/${project.mod_version}"
    mainFile.changelogType = "markdown"

    mainFile.addGameVersion(project.minecraft_version)
    mainFile.addModLoader("Fabric")

    // Ensure remapJar is finished before this runs
    dependsOn(remapJar)
}